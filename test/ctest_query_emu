#!/usr/bin/perl
# Copyright 2006-2008 The FLWOR Foundation.
#  
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#  
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Call with argument -h for information

use strict;
use warnings;

use File::Find;
use File::Path;
use Cwd 'abs_path';
use IO::Handle;

sub usage {
    print "Emulates ctest operation for test/rbkt/ tests.\n";
    print "Arguments: BUILD_DIR [--suite SUITE_DIR] [-b BUCKET] [-E EXCLUDE_PATTERN] [-R INCLUDE_PATTERN]\n";
}

my $rbktdir = `dirname $0`; chomp $rbktdir; $rbktdir = "$rbktdir/rbkt";
my $ext_suite = 0;
my $builddir = shift;
if (! defined $builddir || ($builddir eq "-h") || ($builddir eq "--help") || ! -e $builddir)
{ usage; exit (1); }
$builddir = abs_path ($builddir) || die $!;
my $bucket = ".";
my $exclude;
my $include;
my $verbose;

while (1) {  # iterate over args
    my $arg = shift;
    if (! defined ($arg)) { last; }
    if ($arg eq "-b") { $bucket = shift; }
    elsif ($arg eq "-V") { $verbose = 1; }
    elsif ($arg eq "-E") { $exclude = shift; }
    elsif ($arg eq "-R") { $include = shift; }
    elsif ($arg eq "--suite") {
        $rbktdir = shift;
        $ext_suite = 1;
    }
    else { die; }
}

-e $rbktdir || die $!;
$rbktdir = abs_path ($rbktdir);

my $testdriver = "$builddir/test/rbkt/testdriver";
-f $testdriver || die $!;
my $logfile = "$builddir/Testing/Temporary/LastTest.log";

mkpath "$builddir/Testing/Temporary";
open (LOG, ">$logfile") || die $!;
# LOG->autoflush (1);

my @failed;
my $testcnt = 0;

sub finish {
    close LOG;
    print "\n\n";
    if (@failed > 0) {
        print ("Failed tests:\n" . join ("\n", @failed) . "\n");
    } else {
        print "All tests passed\n";
    }
    open SUMMARY, ">$builddir/Testing/Temporary/LastTestsFailed.log";
    print SUMMARY (join ("\n", @failed) . "\n");
    close SUMMARY;
}

sub ffun {
    my $fname = $_;
    if (! ($fname =~ m/[.]xq$/)) { return 1; }
    ++$testcnt;
    my $f = $File::Find::name; $f =~ s/[.]xq$//g; $f =~ s@^[.]/@@;
    if (defined ($exclude) && $f =~ m/$exclude/) { return; }
    if (defined ($include) && ! ($f =~ m/$include/)) { return; }
    my $cmd = "$testdriver";
    if ($ext_suite) { $cmd = "$cmd --rbkt-src $rbktdir"; }
    $cmd = "$cmd $f.xq";
    print "$testcnt $f... ";
    print LOG "--- Start test $f\n";
    print LOG "Command: $cmd\n";
    close LOG;
    my $rc = system ("$cmd >${logfile}.tmp 2>&1");
    open (LOG, ">>$logfile") || die $!;
    open (QLOG, "<${logfile}.tmp");
    while (<QLOG>) {
        print LOG;
        if ($verbose) { print; }
    }
    close QLOG;
    unlink "${logfile}.tmp";
    
    if (($rc & 127) == 2) {
        print "Signal " . ($rc & 127) . "\n";
        finish;
        exit (1);
    }
    if ($verbose) { print "Test $f"; }
    if ($rc != 0) {
        print "\tFAILED\n";
        print LOG "Test failed (code " . ($? >> 8) . ")\n";
        push (@failed, "$testcnt:$f");
    } else {
        print "\tPASSED\n";
    }
    print LOG "\n\n--- End test $f\n\n";
}

$SIG{'INT'} = 'CLEANUP';
sub CLEANUP {
    print "Signal 2\n";
    print LOG "--- Testing interrupted\n";
    finish;
    exit (1);
}

chdir ("$rbktdir/Queries") || die $!;
finddepth ({ wanted => \&ffun, follow_fast => 1 }, "$bucket/");
finish;

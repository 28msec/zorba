# Copyright 2006-2008 The FLWOR Foundation.
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
# http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

ADD_SUBDIRECTORY(Scripts)
ADD_SUBDIRECTORY(ExpCompilerResults)

# create the result directory for all tests
IF (NOT EXISTS ${CMAKE_CURRENT_BINARY_DIR}/QueryResults/)
    FILE(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/QueryResults/)
ENDIF (NOT EXISTS ${CMAKE_CURRENT_BINARY_DIR}/QueryResults/)

# configure the testdriver
CONFIGURE_FILE(testdriverconfig.h.in ${CMAKE_CURRENT_BINARY_DIR}/testdriverconfig.h)

#
# Compile and build the testdriver executable
#
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_BINARY_DIR})

ADD_EXECUTABLE(testdriver testdriver.cpp)
TARGET_LINK_LIBRARIES(testdriver zorba_simplestore)

IF (ZORBA_BUILD_STATIC_LIBRARY)
  ADD_EXECUTABLE(testdriver_static testdriver.cpp)
  SET_TARGET_PROPERTIES(testdriver_static PROPERTIES COMPILE_DEFINITIONS BUILDING_ZORBA_STATIC)
  TARGET_LINK_LIBRARIES(testdriver_static zorba_simplestore_static)
ENDIF (ZORBA_BUILD_STATIC_LIBRARY)

#
# Compile and build the multi-threaded testdriver executable
#
#ADD_EXECUTABLE(testdriver_mt testdriver_mt.cpp)

#TARGET_LINK_LIBRARIES(testdriver_mt zorba)

IF(WIN32)
#for Windows and Cygwin
  SET(TESTDRIVER_EXE_PATH   ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/testdriver)
ELSE(WIN32)
  SET(TESTDRIVER_EXE_PATH   testdriver)
ENDIF(WIN32)
 

MESSAGE(STATUS "Adding tests for CTest")

# collect all queries (suffix .xq) in all subdirectories of the Queries dir
FILE(GLOB_RECURSE TESTFILES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}/Queries/ *.xq)

# collect all chained test queries (suffix .cxq) in all subdirectories of the Queries dir
FILE(GLOB_RECURSE CHAINED_TESTFILES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}/Queries/ *.cxq)

SET(TESTCOUNTER 0)

LIST(LENGTH CHAINED_TESTFILES cxqNO)

# if we have .cxq files
IF(${cxqNO} STRGREATER "0")

  SET(PARAM 0)

  #create a list with the folders containg *.cxq files
  FOREACH(CHAINED_TESTFILE ${CHAINED_TESTFILES})
    GET_FILENAME_COMPONENT(TF_PATH ${CHAINED_TESTFILE} PATH)
    IF(NOT ${PARAM} STREQUAL ${TF_PATH})
      LIST(APPEND CHAINED_TESTS_LIST ${TF_PATH})
    ENDIF(NOT ${PARAM} STREQUAL ${TF_PATH})
    SET(PARAM ${TF_PATH})
  ENDFOREACH(CHAINED_TESTFILE)

  #foreach chained test create a list with all the test files and sort it alphabetically
  FOREACH(CT ${CHAINED_TESTS_LIST})

    #create the name of the test and put it in TEST_NAME variable
    MATH(EXPR TESTCOUNTER ${TESTCOUNTER}+1)
    GET_FILENAME_COMPONENT(TF_DIR ${CT} NAME)
    SET(TEST_NAME "${TESTCOUNTER}_${TF_DIR}")

    #create the list of tests to be passed to the testdriver call
    FOREACH(CHAINED_TESTFILE ${CHAINED_TESTFILES})
      GET_FILENAME_COMPONENT(TF_PATH ${CHAINED_TESTFILE} PATH)
      IF(TF_PATH STREQUAL CT)
		IF(ZORBA_USE_SEPARATE_WINCE_SVN)
		  LIST(APPEND CHAINED_TESTS_FILES ${CMAKE_CURRENT_SOURCE_DIR}/Queries/${CHAINED_TESTFILE})
		ELSE(ZORBA_USE_SEPARATE_WINCE_SVN)
		  GET_FILENAME_COMPONENT(TF_NAME ${CHAINED_TESTFILE} NAME)
		  LIST(APPEND CHAINED_TESTS_FILES ${CHAINED_TESTFILE})
		ENDIF(ZORBA_USE_SEPARATE_WINCE_SVN)
      ENDIF(TF_PATH STREQUAL CT)
    ENDFOREACH(CHAINED_TESTFILE)

    #sort the list of test files alphabetically
    LIST(SORT  CHAINED_TESTS_FILES)

  SET(TEST_NAME "test/rbkt/${TEST_NAME}")
  #finally add the test
	IF(ZORBA_USE_SEPARATE_WINCE_SVN)
	  ADD_TEST(${TEST_NAME} ${TESTDRIVER_EXE_PATH} ${CMAKE_CURRENT_BINARY_DIR} ${CHAINED_TESTS_FILES})
	ELSE(ZORBA_USE_SEPARATE_WINCE_SVN)
	  ADD_TEST(${TEST_NAME} ${TESTDRIVER_EXE_PATH} ${CHAINED_TESTS_FILES})
	ENDIF(ZORBA_USE_SEPARATE_WINCE_SVN)

    SET_TESTS_PROPERTIES (${TEST_NAME} PROPERTIES TIMEOUT "10")

    #clear the list of testfiles, args and also the name of the test
    SET(CHAINED_TESTS_FILES)
    SET(ARGS)
    SET(TEST_NAME)
  ENDFOREACH(CT)
ENDIF(${cxqNO} STRGREATER "0")

FOREACH(TESTFILE ${TESTFILES})

  STRING(LENGTH ${TESTFILE} TESTFILELENGTH)
  MATH(EXPR TESTLENGTH '${TESTFILELENGTH}-3' )
  STRING(SUBSTRING ${TESTFILE} 0 ${TESTLENGTH} TESTNAME)
  SET(TESTNAME "test/rbkt/${TESTNAME}")

  IF(ZORBA_USE_SEPARATE_WINCE_SVN)
    ADD_TEST(${TESTNAME} ${TESTDRIVER_EXE_PATH} ${CMAKE_CURRENT_BINARY_DIR} ${CMAKE_CURRENT_SOURCE_DIR}/Queries/${TESTFILE})
  ELSE(ZORBA_USE_SEPARATE_WINCE_SVN)
    ADD_TEST(${TESTNAME} ${TESTDRIVER_EXE_PATH} ${TESTFILE})
  ENDIF(ZORBA_USE_SEPARATE_WINCE_SVN)

  SET_TESTS_PROPERTIES (${TESTNAME} PROPERTIES TIMEOUT "10")

  MATH(EXPR TESTCOUNTER ${TESTCOUNTER}+1)
  MATH(EXPR TESTMOD "${TESTCOUNTER}/1000")
  IF (${TESTMOD})
    MESSAGE(STATUS "Adding another 1000 Tests")
    SET(TESTCOUNTER 0)
  ENDIF (${TESTMOD})

ENDFOREACH(TESTFILE)

SET_TESTS_PROPERTIES (test/rbkt/dblp/q0 PROPERTIES TIMEOUT "200")
SET_TESTS_PROPERTIES (test/rbkt/dblp/q1 PROPERTIES TIMEOUT "200")

IF(ZORBA_NO_BIGNUMBERS)
  SET_TESTS_PROPERTIES (test/rbkt/Operations/ArithExpr/DurationArith/DayTimeDurationDivideDTD/DayTimeDurationDivideDTD1 PROPERTIES WILL_FAIL TRUE)
ENDIF(ZORBA_NO_BIGNUMBERS)

IF(ZORBA_NO_XMLSCHEMA)
  SET_TESTS_PROPERTIES (test/rbkt/schemas/userDefinedAtomicType01 PROPERTIES WILL_FAIL TRUE)
ENDIF(ZORBA_NO_XMLSCHEMA)

IF(ZORBA_NO_UNICODE)
  SET_TESTS_PROPERTIES (test/rbkt/string/CodepointToStringFunc/CodepointToStringFunc1 PROPERTIES WILL_FAIL TRUE)
  SET_TESTS_PROPERTIES (test/rbkt/string/CompareFunc/CompareFunc3 PROPERTIES WILL_FAIL TRUE)
  SET_TESTS_PROPERTIES (test/rbkt/string/StringToCodepointFunc/StringToCodepointFunc1 PROPERTIES WILL_FAIL TRUE)
ENDIF(ZORBA_NO_UNICODE)


module namespace auctions = "http://www.w3.org/TestModules/auctions";

import module namespace xqddf = "http://www.zorba-xquery.com/modules/xqddf";

declare variable $auctions:auctions := xs:QName("auctions:auctions");
declare variable $auctions:PersonId := xs:QName("auctions:PersonId");
declare variable $auctions:PersonCity := xs:QName("auctions:PersonCity");

declare collection auctions:auctions;


(:
  The PersonId index maps each id to the person with that id.
:)

declare unique value range index auctions:PersonId
on nodes xqddf:collection(xs:QName("auctions:auctions"))/site/people/person
by (fn:data(./@id)) as xs:string;


(:
  The PersonCity index maps each city to the persons that live in that city.
  The PersonCity index contains some NULL keys because not all persons have a city 
:)

declare value range automatically maintained index auctions:PersonCity
on nodes xqddf:collection(xs:QName("auctions:auctions"))/site/people/person
by ((.//city)) as xs:string?;


(:
  Create and populate the collection, and then create the indexes
:)

declare sequential function auctions:create-db()
{
  xqddf:create-collection($auctions:auctions);

  xqddf:insert-nodes-last($auctions:auctions, doc("auctions1.xml"));

  xqddf:create-index($auctions:PersonId);

  xqddf:create-index($auctions:PersonCity);
};


declare function auctions:probe-point-id($indexName as xs:QName, $id as xs:string)
{
  xqddf:probe-index-point($indexName, $id)
};


(:
  Look for persons whose id is >= than the given id
:)

declare function auctions:probe-range-id($indexName as xs:QName, $id as xs:string)
{
  xqddf:probe-index-range($indexName,
                        $id,
                        1,
                        xs:boolean("true"), 
                        xs:boolean("false"),
                        xs:boolean("true"),
                        xs:boolean("false"))
};


declare function auctions:probe-point-city($indexName as xs:QName, $city as xs:string?)
{
  xqddf:probe-index-point($indexName, $city)
};


(:
  Look for persons whose city is >= than the given city
:)

declare function auctions:probe-range-city($indexName as xs:QName, $city as xs:string)
{
  xqddf:probe-index-range($indexName,
                        $city,
                        1,
                        xs:boolean("true"),
                        xs:boolean("false"),
                        xs:boolean("true"),
                        xs:boolean("false"))
};


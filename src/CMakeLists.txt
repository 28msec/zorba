INCLUDE(${CMAKE_SOURCE_DIR}/cmake_modules/AddSrcSubfolder.cmake)

INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_SOURCE_DIR})
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_BINARY_DIR})
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_SOURCE_DIR}/external) # must be removed if pthread.h is removed from util/rwlock.h/cpp

ADD_SUBDIRECTORY(common)

# generate the zorbatypes library
ADD_SUBDIRECTORY(zorbatypes)

IF (ZORBA_BUILD_STATIC_LIBRARY)
  SET (requiredlibs ${requiredlibs} zorbatypes_static)
ELSE (ZORBA_BUILD_STATIC_LIBRARY)
  SET (requiredlibs ${requiredlibs} zorbatypes)
ENDIF (ZORBA_BUILD_STATIC_LIBRARY)

# generate the zorba library
SET(ZORBA_SRCS)

############################################################ Add files compiled into the library
ADD_SRC_SUBFOLDER(ZORBA_SRCS api API_SRCS )
FOREACH (SRCS ${API_BUILD_SRCS})
  LIST(APPEND ZORBA_SRCS ${SRCS})
ENDFOREACH (SRCS)
ADD_SRC_SUBFOLDER(ZORBA_SRCS compiler COMPILER_SRCS)
FOREACH (SRCS ${COMPILER_BUILD_SRCS})
  LIST(APPEND ZORBA_SRCS ${SRCS})
ENDFOREACH (SRCS)
ADD_SRC_SUBFOLDER(ZORBA_SRCS system SYSTEM_SRCS)
ADD_SRC_SUBFOLDER(ZORBA_SRCS context CONTEXT_SRCS)
ADD_SRC_SUBFOLDER(ZORBA_SRCS errors	ERRORS_SRCS)
ADD_SRC_SUBFOLDER(ZORBA_SRCS functions FUNCTIONS_SRCS)
ADD_SRC_SUBFOLDER(ZORBA_SRCS runtime RUNTIME_SRCS)
ADD_SRC_SUBFOLDER(ZORBA_SRCS store STORE_SRCS)
ADD_SRC_SUBFOLDER(ZORBA_SRCS types TYPES_SRCS)
ADD_SRC_SUBFOLDER(ZORBA_SRCS util UTIL_SRCS)
#ADD_SRC_SUBFOLDER(ZORBA_SRCS sax2 SAX2_SRCS)

############################################################ Zorba library
IF (MSVC)
  ADD_LIBRARY(zorba ${ZORBA_SRCS})
ELSE (MSVC)
  ADD_LIBRARY(zorba SHARED ${ZORBA_SRCS})
ENDIF (MSVC)

TARGET_LINK_LIBRARIES(zorba ${requiredlibs})
SET_TARGET_PROPERTIES(zorba PROPERTIES VERSION ${ZORBA_MAJOR_NUMBER}.${ZORBA_MINOR_NUMBER}.${ZORBA_PATCH_NUMBER})
SET_TARGET_PROPERTIES(zorba PROPERTIES CLEAN_DIRECT_OUTPUT 1)
SET(ZORBA_LIBRARY_INSTALL_LIST ${ZORBA_LIBRARY_INSTALL_LIST} zorba)


############################################################ Zorba STATIC library
IF (ZORBA_BUILD_STATIC_LIBRARY)
  ADD_LIBRARY(zorba_static STATIC ${ZORBA_SRCS})
  TARGET_LINK_LIBRARIES(zorba_static ${requiredlibs})
  SET_TARGET_PROPERTIES(zorba_static PROPERTIES OUTPUT_NAME "zorba_static")  
  SET_TARGET_PROPERTIES(zorba_static PROPERTIES VERSION ${ZORBA_MAJOR_NUMBER}.${ZORBA_MINOR_NUMBER}.${ZORBA_PATCH_NUMBER})  
  SET_TARGET_PROPERTIES(zorba_static PROPERTIES CLEAN_DIRECT_OUTPUT 1)  
  SET(ZORBA_LIBRARY_INSTALL_LIST ${ZORBA_LIBRARY_INSTALL_LIST} zorba_static)
ENDIF (ZORBA_BUILD_STATIC_LIBRARY)

INSTALL(TARGETS ${ZORBA_LIBRARY_INSTALL_LIST}
  RUNTIME DESTINATION bin
  LIBRARY DESTINATION lib
  ARCHIVE DESTINATION lib
)

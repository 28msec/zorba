xquery version "3.0";

(:
 : Copyright 2006-2011 The FLWOR Foundation.
 :
 : Licensed under the Apache License, Version 2.0 (the "License");
 : you may not use this file except in compliance with the License.
 : You may obtain a copy of the License at
 :
 : http://www.apache.org/licenses/LICENSE-2.0
 :
 : Unless required by applicable law or agreed to in writing, software
 : distributed under the License is distributed on an "AS IS" BASIS,
 : WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 : See the License for the specific language governing permissions and
 : limitations under the License.
:)

module namespace conf  = "http://www.zorba-xquery.com/modules/conformance";

import module namespace file = "http://expath.org/ns/file";
import module namespace functx = "http://www.functx.com/";

import module namespace dml = "http://www.zorba-xquery.com/modules/store/static/collections/dml";
import module namespace ddl = "http://www.zorba-xquery.com/modules/store/static/collections/ddl";

import schema namespace output = "http://www.w3.org/2010/xslt-xquery-serialization";

declare namespace ann = "http://www.zorba-xquery.com/annotations";
declare namespace xqCat = "http://www.w3.org/2005/02/query-test-XQTSCatalog";
declare namespace xqRes = "http://www.w3.org/2005/02/query-test-XQTSResult";

declare %private variable $conf:submissionXqueryX as xs:QName := xs:QName("conf:submissionXqueryX");
declare collection conf:submissionXqueryX as node()*;

declare %private variable $conf:submissionXquery as xs:QName := xs:QName("conf:submissionXquery");
declare collection conf:submissionXquery as node()*;

declare %private variable $conf:failingXQuery as xs:QName := xs:QName("conf:failingXQuery");
declare collection conf:failingXQuery as node()*;

declare %private variable $conf:failingXQueryX as xs:QName := xs:QName("conf:failingXQueryX");
declare collection conf:failingXQueryX as node()*;

declare %private variable $conf:serParamXml :=
  <output:serialization-parameters>
    <output:method value="xml"/>
    <output:indent value="yes"/>
  </output:serialization-parameters>;
  
declare %private variable $conf:serParamXhtml :=
  <output:serialization-parameters>
    <output:method value="xhtml"/>
    <output:indent value="yes"/>
  </output:serialization-parameters>;
  
declare %private variable $conf:XQTS_PATH  as xs:string := "/tmp/XQTS_current";
declare %private variable $conf:XQTS_submission as xs:string := 
        "/tmp/XQTS_current/ReportingResults/zorba_11674_xqts_current_xquery_1_0.xml";
declare %private variable $conf:XQTSX_submission as xs:string := 
        "/tmp/XQTS_current/ReportingResults/zorba_11674_xqtsx_current_xquery_1_0.xml";

declare %private variable $conf:XQuery   as xs:string := "XQuery";
declare %private variable $conf:XQueryX  as xs:string := "XQueryX";

declare %private variable $conf:features := <features/>;

declare %private variable $conf:numbers := <numbers/>;

declare %private %ann:sequential function conf:collect-feature(
  $name as xs:string, 
  $supported as xs:string) 
{
  insert node <feature name ="{$name}" supported="{$supported}" /> as last into $conf:features;
};

declare %private %ann:sequential function conf:collect-number(
  $name   as xs:string, 
  $value  as xs:string) 
{
  insert node <number name ="{$name}" value="{$value}" /> as last into $conf:numbers;
};

declare %private function  conf:value-intersect(
  $arg1 as xs:anyAtomicType*,
  $arg2 as xs:anyAtomicType*) as xs:anyAtomicType* 
{
  fn:distinct-values($arg1[.=$arg2])
};

declare function conf:count-available-tests($node, $isDetailed as xs:boolean) as xs:integer?
{
  if(not($isDetailed) or
    ($isDetailed and (count($node//xqCat:test-group) eq xs:integer(0)))) then
    count($node//xqCat:test-case)
  else
    ()
};

declare function conf:failing-tests($node, $no_spaces as xs:integer, $isDetailed as xs:boolean)
{
  let $padding := functx:repeat-string(codepoints-to-string(160), $no_spaces)
  return
    if(not($isDetailed) or 
      ($isDetailed and (count($node//xqCat:test-group) eq xs:integer(0)))) then
    (: if there are no sub-categories and there are tests in this category :)
    {
      (: list all tests that are not passing in either XQuery or XQueryX :)
      for $testCase in $node//xqCat:test-case
      let $testCaseName := data($testCase/@name)
      let $xqTmp  := xs:string(dml:collection(xs:QName("conf:failingXQuery"))[@name = $testCaseName]/@value)
      let $xqStatus := if(exists($xqTmp)) then $xqTmp else "pass"

      let $xqxTmp := xs:string(dml:collection(xs:QName("conf:failingXQueryX"))[@name = $testCaseName]/@value)
      let $xqxStatus := if(exists($xqxTmp)) then $xqxTmp else "pass"
      where (exists(dml:collection( xs:QName("conf:failingXQuery"))[@name = $testCaseName]/@value) or
             exists(dml:collection( xs:QName("conf:failingXQueryX"))[@name = $testCaseName]/@value))
      return
        if(($xqStatus = "pass") and ($xqxStatus = "pass")) then ()
        else
          <tr bgcolor="lightcyan">
            <td >{$padding}
              <a target="_blank" href="{concat('http://dev.w3.org/cvsweb/~checkout~/2006/xquery-test-suite/TestSuiteStagingArea/Queries/XQuery/',data($testCase/@FilePath),$testCaseName,'.xq')}">{$testCaseName}</a>
            </td>
            <td class="xq" bgcolor="{if($xqStatus = 'fail') then 'coral' else if($xqStatus = 'pass') then 'mediumseagreen' else 'lightcyan'}">
              {$xqStatus}</td>
            <td class="xqx" bgcolor="{if($xqxStatus = 'fail') then 'coral' else if($xqxStatus = 'pass') then 'mediumseagreen' else 'lightcyan'}">
              {$xqxStatus}</td>
          </tr>
    }
    else ()
};

declare function conf:create-tr(
  $nodes, 
  $no_spaces as xs:integer,
  $StaticTyping as xs:boolean)
{
  let $padding := functx:repeat-string(codepoints-to-string(160), $no_spaces)
  return
    for $test-group in $nodes
    let $failingTests := conf:failing-tests($test-group, ($no_spaces + xs:integer(2)), fn:true())
    
    let $total := conf:count-available-tests($test-group, fn:true())
    let $passingXQ := $total - count(for $test in $failingTests//td[@class = "xq"] 
                                     where (not($test/text()='pass') and 
                                           not($test/text()='not applicable') and 
                                           not($test/text()='no result'))
                                     return $test)
    let $notApplXQ := count(for $test in $failingTests//td[@class = "xq"]
                            where  ($test/text()='not applicable')
                            return $test)
    let $sumXQ := $passingXQ + $notApplXQ
                            
    let $passingXQX := $total - count(for $test in $failingTests//td[@class = "xqx"] 
                                     where (not($test/text()='pass') and
                                           not($test/text()='not applicable') and 
                                           not($test/text()='no result') )
                                     return $test)
    let $notApplXQX := count(for $test in $failingTests//td[@class = "xqx"]
                            where  ($test/text()='not applicable')
                            return $test)
    let $sumXQX := $passingXQX + $notApplXQX
    let $isStaticTyping as xs:boolean := fn:contains(data($test-group/xqCat:GroupInfo/xqCat:title),"Static Typing")
    return
    (
     if(count($test-group//xqCat:test-case) ne xs:integer(0)) then
      <tr bgcolor="lightcyan">
        <td bgcolor="{if(not(exists($total))) then 'paleturquoise' else 'lightcyan'}">
          {concat($padding,data($test-group/xqCat:GroupInfo/xqCat:title))}</td>
        <td align="center" bgcolor="{if(not(exists($total))) then 'paleturquoise'
                      else if($sumXQ = $total) then 'mediumseagreen' 
                      else if($isStaticTyping) then 'white'
                      else if(($sumXQ div $total)*100 > 25) then 'palegreen' else 'coral'}">
          {if(exists($total) and ($total ne xs:integer(0))) 
           then concat($sumXQ,"/",$total) else ()}</td>
        <td align="center" bgcolor="{if(not(exists($total))) then 'paleturquoise' 
                      else if($sumXQX = $total) then 'mediumseagreen'
                      else if($isStaticTyping) then 'white' 
                      else if(($sumXQX div $total)*100 > 25) then 'palegreen' else 'coral'}">
          {if(exists($total) and ($total ne xs:integer(0))) 
           then concat($sumXQX,"/",$total) else ()}</td>
      </tr> else (),
      conf:create-tr($test-group/xqCat:test-group, 
                     ($no_spaces + xs:integer(2)),
                     $isStaticTyping)
    )
};

declare %private function conf:round-decimal(
  $tmp  as xs:decimal) as xs:decimal
{
  let $dec := ($tmp - floor($tmp))*100
  return (floor($tmp)+(round($dec) div 100))
};

declare %private function conf:is-root-category(
  $catName as xs:string, 
  $XQTS_catalog) as xs:boolean
{
  let $rootCategories := for $test-group in $XQTS_catalog/xqCat:test-suite/xqCat:test-group
                         return xs:string($test-group/xqCat:GroupInfo/xqCat:title)
  return exists(conf:value-intersect($rootCategories, $catName))
};

declare %ann:sequential function conf:create-tr-summary(
  $nodes, 
  $no_spaces as xs:integer,
  $StaticTyping as xs:boolean,
  $XQTS_catalog)
{
  let $padding := functx:repeat-string(codepoints-to-string(160), $no_spaces)
  return
    for $test-group in $nodes
    let $failingTests := conf:failing-tests($test-group, ($no_spaces + xs:integer(2)), fn:false())
    let $total := conf:count-available-tests($test-group, fn:false())
    let $passingXQ := $total - count(for $test in $failingTests//td[@class = "xq"] 
                                     where (not($test/text()='pass') and 
                                           not($test/text()='not applicable') and 
                                           not($test/text()='no result')) 
                                     return $test)
    let $notApplXQ := count(for $test in $failingTests//td[@class = "xq"]
                            where  ($test/text()='not applicable')
                            return $test)
    let $sumXQ := $passingXQ + $notApplXQ
                            
    let $passingXQX := $total - count(for $test in $failingTests//td[@class = "xqx"] 
                                     where (not($test/text()='pass') and
                                           not($test/text()='not applicable') and 
                                           not($test/text()='no result') )
                                     return $test)
    let $notApplXQX := count(for $test in $failingTests//td[@class = "xqx"]
                            where  ($test/text()='not applicable')
                            return $test)
    let $sumXQX := $passingXQX + $notApplXQX
    let $isStaticTyping as xs:boolean := fn:contains(data($test-group/xqCat:GroupInfo/xqCat:title),"Static Typing")
    let $percentXQ := conf:round-decimal(($passingXQ div $total)*100)
    let $percentXQX := conf:round-decimal(($passingXQX div $total)*100)  
    return
    (if(conf:is-root-category(data($test-group/xqCat:GroupInfo/xqCat:title), $XQTS_catalog)) then
      conf:collect-number(data($test-group/xqCat:GroupInfo/xqCat:title), xs:string($percentXQ))
     else ()      
     ,
     if(count($test-group//xqCat:test-case) ne xs:integer(0)) then
      <tr bgcolor="lightcyan">
        <td bgcolor="{if(not(exists($total))) then 'paleturquoise' else 'lightcyan'}">
          {concat($padding,data($test-group/xqCat:GroupInfo/xqCat:title))}</td>
        <td align="center" bgcolor="{if(not(exists($total))) then 'paleturquoise'
                      else if($sumXQ = $total) then 'mediumseagreen'
                      else if($isStaticTyping) then 'white'
                      else if(($sumXQ div $total)*100 > 25) then 'palegreen' else 'coral'}">
          {if(exists($total) and ($total ne xs:integer(0))) 
           then concat($sumXQ,"/",$total) else ()}<br />
           {if(conf:is-root-category(data($test-group/xqCat:GroupInfo/xqCat:title), $XQTS_catalog)) then concat($percentXQ,'%') else ()}</td>
        <td  align="center" bgcolor="{if(not(exists($total))) then 'paleturquoise' 
                      else if($sumXQX = $total) then 'mediumseagreen'
                      else if($isStaticTyping) then 'white'
                      else if(($sumXQX div $total)*100 > 25) then 'palegreen' else 'coral'}">
          {if(exists($total) and ($total ne xs:integer(0))) 
           then concat($sumXQX,"/",$total) else ()}<br />
           {if(conf:is-root-category(data($test-group/xqCat:GroupInfo/xqCat:title), $XQTS_catalog)) then concat($percentXQ,'%') else ()}</td>
      </tr> else (),
      if(data($test-group/@name) = "Optional") then
      conf:create-tr-summary($test-group/xqCat:test-group, 
                            ($no_spaces + xs:integer(2)),
                            $isStaticTyping,
                            $XQTS_catalog)
      else ()
    )
};

declare %ann:nondeterministic %ann:sequential function conf:create-collection($syntax as xs:string, $XQTS_catalog)
{
  variable $noTestsCatalog := count($XQTS_catalog//*:test-case);
  
  if($syntax eq $conf:XQuery) then
  {
    ddl:create($conf:failingXQuery);

    variable $noTestsSubmissionXQuery := count(dml:collection(xs:QName("conf:submissionXquery")));
    
    if($noTestsCatalog > $noTestsSubmissionXQuery) then
    {
      variable $untested as xs:integer := trace(($noTestsCatalog - $noTestsSubmissionXQuery),
                                          "tests that are not reported in the XQuery submission file and have to be identified...");
      (: there are tests for which we do not report anything :)
      
      for $testCatalog in $XQTS_catalog//*:test-case
      where empty(dml:collection(xs:QName("conf:submissionXquery"))[@name = data($testCatalog/@name)])
      return
        dml:apply-insert-nodes($conf:failingXQuery, <test name="{data($testCatalog/@name)}" value="no result"/>);
        
    }
    else ();
    
    for $testFailed in dml:collection(xs:QName("conf:submissionXquery"))
    let $name := data($testFailed/@name)
    let $status := data($testFailed/@result)
    let $comment := data($testFailed/@comment)
    where (not($status = "pass") and not($status = "not applicable"))
    return
      if(exists($comment)) then
        dml:apply-insert-nodes($conf:failingXQuery, <test name="{$name}" value="{$status}" comment="{$comment}"/>);
      else
        dml:apply-insert-nodes($conf:failingXQuery, <test name="{$name}" value="{$status}"/>);
  }
  else if($syntax eq $conf:XQueryX) then
  {
    ddl:create($conf:failingXQueryX);
    
    variable $noTestsSubmissionXQueryX := count(dml:collection(xs:QName("conf:submissionXqueryX")));
    if($noTestsCatalog > $noTestsSubmissionXQueryX) then
    {
      variable $untested as xs:integer := trace(($noTestsCatalog - $noTestsSubmissionXQueryX),
                                          "tests that are not reported in the XQueryX submission file and have to be identified..."); 
      (: there are tests for which we do not report anything :)      
      for $testCatalog in $XQTS_catalog//*:test-case
      where empty(dml:collection(xs:QName("conf:submissionXqueryX"))[@name = data($testCatalog/@name)])
      return
        dml:apply-insert-nodes($conf:failingXQueryX, <test name="{data($testCatalog/@name)}" value="no result"/>);
    }
    else ();
    
    for $testFailed in dml:collection(xs:QName("conf:submissionXqueryX"))
    let $name := data($testFailed/@name)
    let $status := data($testFailed/@result)
    let $comment := data($testFailed/@comment)
    where (not($status = "pass") and not($status = "not applicable"))
    return
      if(exists($comment)) then
        dml:apply-insert-nodes($conf:failingXQueryX, <test name="{$name}" value="{$status}" comment="{$comment}"/>);
      else
        dml:apply-insert-nodes($conf:failingXQueryX, <test name="{$name}" value="{$status}"/>);
  }
  else
  ();
};

declare %ann:sequential function conf:create-collection-submissions()
{
  ddl:create($conf:submissionXquery);
  
  variable $doc := fn:parse-xml(file:read-text(file:resolve-path($conf:XQTS_submission)));

  for $test in $doc/xqRes:test-suite-result//xqRes:test-case
  return dml:apply-insert-nodes($conf:submissionXquery, $test);

  (: collect features :)
  for $feature in $doc/xqRes:test-suite-result/xqRes:implementation/xqRes:features/xqRes:feature
  return
    conf:collect-feature(data($feature/@name),
                         data($feature/@supported));

  ddl:create($conf:submissionXqueryX);
  
  let $doc := fn:parse-xml(file:read-text(file:resolve-path($conf:XQTSX_submission)))
  return
    for $test in $doc/xqRes:test-suite-result//xqRes:test-case
    return dml:apply-insert-nodes($conf:submissionXqueryX, $test);
};

declare function conf:generate-detailed-table($XQTS_catalog)
{
  <table bgcolor="lightcyan">
    <tr><td>W3C XQuery 1.0 features</td><td>XQuery</td><td>XQueryX</td></tr>
    <tr><td></td><td></td><td></td></tr>
    {
      for $test-group in $XQTS_catalog/xqCat:test-suite/xqCat:test-group
      (: we disable Static Typing explicitelly because there is no clear rule to connect the features with test-groups description :)
      let $isStaticTyping as xs:boolean := fn:contains(data($test-group/xqCat:GroupInfo/xqCat:title),"Static Typing")
      return conf:create-tr($test-group,
                            xs:integer(0),
                            $isStaticTyping)
    }
  </table>
};

declare %ann:sequential function conf:generate-summary-table($XQTS_catalog)
{
  <table bgcolor="lightcyan">
    <tr><td>W3C XQuery 1.0 features</td><td>XQuery</td><td>XQueryX</td></tr>
    <tr><td></td><td></td><td></td></tr>
    {
      for $test-group in $XQTS_catalog/xqCat:test-suite/xqCat:test-group
      (: we disable Static Typing explicitelly because there is no clear rule to connect the features with test-groups description :)
      let $isStaticTyping as xs:boolean := fn:contains(data($test-group/xqCat:GroupInfo/xqCat:title),"Static Typing")
      return conf:create-tr-summary($test-group,
                                    xs:integer(0),
                                    $isStaticTyping,
                                    $XQTS_catalog)
    }
  </table>
};

declare function conf:generate-features-table()
{
  <table bgcolor="lightcyan">
    <tr><td>W3C XQuery 1.0 feature</td><td>supported</td></tr>
    <tr><td></td><td></td></tr>
    {
      for $feature in $conf:features/feature
      let $isSupported := xs:boolean(data($feature/@supported))
      return
      <tr>
        <td>{data($feature/@name)}</td>
        <td bgcolor="{if($isSupported) then 'mediumseagreen' else 'white'}">{data($feature/@supported)}</td>
      </tr>
    }
  </table>
};

declare %private function conf:create-summary-string() as xs:string
{
  let $average := avg(for $number in $conf:numbers/number return xs:decimal(data($number/@value)))
  return
    concat("Zorba achieves ",
            conf:round-decimal($average), 
            "% correctness (",
            string-join((for $number in $conf:numbers/number return concat(xs:string(data($number/@value)),"% on ",xs:string(data($number/@name)))),", "),
            ") for the W3C Test Suite for the XQuery 1.0.")
};

declare %ann:sequential function conf:test()
{  
  (: load the XQTS_catalog as text and replace the relative path to 'cat' with an absolute path :)
  variable $XQTS_catalog_text := replace(file:read-text(file:resolve-path(concat($conf:XQTS_PATH,file:directory-separator(),"XQTSCatalog.xml"))),
                                    '"cat/',
                                    concat('"file://',$conf:XQTS_PATH,file:directory-separator(),"cat",file:directory-separator()));
                                    
  variable $XQTS_catalog := fn:parse-xml($XQTS_catalog_text);

  (: remove the XQuery11 from the reports :)
  delete node $XQTS_catalog/xqCat:test-suite/xqCat:test-group[@name="XQuery11"];
  
  (:file:write(concat($conf:XQTS_PATH,file:directory-separator(),"XQTSCatalogSoso.xml"),
              $XQTS_catalog,
              $conf:serParamXml);:)

  conf:create-collection($conf:XQuery, $XQTS_catalog);
  conf:create-collection($conf:XQueryX, $XQTS_catalog);
  
  variable $pageNameSummary := "conformanceXQTSSummary";
  variable $pathSummary := fn:concat($conf:XQTS_PATH, file:directory-separator(), $pageNameSummary, ".dox");
  file:write(trace($pathSummary,"output summary file path"),
             ("/** \page conformanceXQTSSummary  Summary W3C XQuery 1.0 conformance

",
             {conf:generate-features-table()},"
",
             {conf:generate-summary-table($XQTS_catalog)}
,"
*/"), 
             $conf:serParamXhtml);

  variable $pageNameDetailed := "conformanceXQTS";
  variable $pathDetailed := fn:concat($conf:XQTS_PATH, file:directory-separator(), $pageNameDetailed, ".dox");

  file:write(trace($pathDetailed,"output detailed file path"),
             ("/** \page conformanceXQTS  W3C XQuery 1.0 Conformance.

",
             {conf:create-summary-string()},"
",
             {conf:generate-detailed-table($XQTS_catalog)}
,"
*/"), 
             $conf:serParamXhtml);
};

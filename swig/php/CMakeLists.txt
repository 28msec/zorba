# Copyright 2006-2008 The FLWOR Foundation.
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
# http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

INCLUDE(${CMAKE_MODULE_PATH}/FindPHP5.cmake)
FIND_PACKAGE(PHP5)
IF( NOT PHP5_FOUND )
  MESSAGE(STATUS "PHP5 binding not generated because library and include file not installed.")
ELSE( NOT PHP5_FOUND )
  MESSAGE(STATUS "Generating PHP5 API")
  SET_SOURCE_FILES_PROPERTIES(zorba_api.i PROPERTIES CPLUSPLUS ON)
  INCLUDE_DIRECTORIES(${PHP5_INCLUDES})
  SWIG_ADD_MODULE(zorba_api php5 zorba_api.i)
  IF(APPLE)
    SWIG_LINK_LIBRARIES(zorba_api zorba_simplestore ${CMAKE_SHARED_MODULE_CREATE_C_FLAGS})
  ELSE(APPLE)
    SWIG_LINK_LIBRARIES(zorba_api zorba_simplestore)
  ENDIF(APPLE)

  # The following fix was added because of the changes in CMake 2.8, which have the
  # result of naming the java binding library "liblibzorba_api.so" instead of "libzorba_api.so"
  IF ( LINUX OR CYGWIN )
    SET_TARGET_PROPERTIES( ${SWIG_MODULE_zorba_api_REAL_NAME} PROPERTIES PREFIX "" )
  ENDIF ( LINUX OR CYGWIN )

  STRING(REPLACE ${CMAKE_INSTALL_PREFIX} "" PHP5_INSTALL_PATH ${PHP5_EXTENSION_DIR})
  SET(PHP5_INSTALL_PATH ${CMAKE_INSTALL_PREFIX}${PHP5_INSTALL_PATH})
  MESSAGE(STATUS "PHP module install path: " ${PHP5_INSTALL_PATH})

  INSTALL ( FILES ${CMAKE_CURRENT_BINARY_DIR}/zorba_api_wrapper.php
                  ${CMAKE_CURRENT_BINARY_DIR}/zorba_api.so
            DESTINATION ${PHP5_INSTALL_PATH} )
  ###
  # Start PHP proxy generation
  ###
  #Configure the test file
  SET(phpAPIPath ${CMAKE_CURRENT_BINARY_DIR})
  CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/generate_proxy.php.in ${CMAKE_CURRENT_BINARY_DIR}/generate_proxy.php)

  ADD_CUSTOM_COMMAND(
    OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/zorba_api_wrapper.php
    # the following addes a dependency to the *.cxx file that is generated by swig
    DEPENDS ${swig_generated_file_fullname}
    DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/generate_proxy.php.in
    COMMAND ${PHP5_EXECUTABLE} 
    ARGS ${CMAKE_CURRENT_BINARY_DIR}/generate_proxy.php
  )
  ADD_CUSTOM_TARGET(php_wrapper ALL
    DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/zorba_api_wrapper.php
  )
  ### End PHP proxy generation

ENDIF( NOT PHP5_FOUND )
